apply plugin: 'com.android.application'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'koin'


android {
	compileSdkVersion 31
	buildToolsVersion '31.0.0'

	defaultConfig {
		applicationId "de.christinecoenen.code.zapp"
		minSdkVersion 23
		targetSdkVersion 31
		versionCode 52
		versionName "5.0.2"
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_1_8.toString()
		freeCompilerArgs += [
			'-Xopt-in=kotlin.RequiresOptIn',
		]
	}

	kapt {
		arguments {
			arg("room.schemaLocation", "$projectDir/schemas")
		}
	}

	buildFeatures {
		viewBinding {
			enabled = true
		}
	}

	buildTypes {
		debug {
			shrinkResources false
		}
		release {
			shrinkResources true
			minifyEnabled true
			proguardFiles getDefaultProguardFile(
				'proguard-android-optimize.txt'),
				'proguard-rules.pro'
		}
	}

	testOptions {
		unitTests {
			includeAndroidResources = true
			all {
				testLogging {
					events 'passed', 'skipped', 'failed', 'standardError'
					exceptionFormat 'full'
				}

				if (project.hasProperty('ci')) {
					// tests that should *not* run on ci machines:
					exclude '**/JsonChannelListConnectionTest*'
				}
			}
		}
	}
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')

	// kotlin
	implementation 'androidx.core:core-ktx:1.7.0'

	// tests
	debugImplementation 'androidx.test:core:1.4.0'
	debugImplementation 'androidx.fragment:fragment-testing:1.3.6'

	// device tests
	androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
	androidTestImplementation 'androidx.test:runner:1.4.0'
	androidTestImplementation 'androidx.test:rules:1.4.0'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
	androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'

	// unit tests
	testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
	testImplementation 'androidx.test:core-ktx:1.4.0'
	testImplementation 'org.robolectric:robolectric:4.6.1'
	testImplementation 'org.mockito.kotlin:mockito-kotlin:3.2.0'
	testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1'

	// support
	implementation 'androidx.fragment:fragment-ktx:1.3.6'
	implementation 'androidx.appcompat:appcompat:1.3.1'
	implementation 'com.google.android.material:material:1.4.0'
	implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
	implementation 'androidx.cardview:cardview:1.0.0'
	implementation 'androidx.annotation:annotation:1.3.0'
	implementation 'androidx.recyclerview:recyclerview:1.2.1'
	implementation 'androidx.preference:preference-ktx:1.1.1'
	implementation "androidx.paging:paging-runtime-ktx:3.0.1"

	def lifecycle_version = "2.4.0"
	implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
	implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"

	// room database
	def room_version = "2.3.0"
	implementation "androidx.room:room-runtime:$room_version"
	kapt "androidx.room:room-compiler:$room_version"
	implementation "androidx.room:room-ktx:$room_version"

	// helper
	implementation 'com.google.code.gson:gson:2.8.7'

	// We need version 2.6 here - newest version still running with Java 7.
	// Newer versions will crash on older devices.
	//noinspection NewerVersionAvailable,GradleDependency
	implementation 'commons-io:commons-io:2.6'

	// sortable list
	implementation 'com.github.woxthebox:draglistview:1.7.2'

	// markdown
	implementation 'ru.noties:markwon:2.0.2'

	// fetch download manager
	implementation 'androidx.tonyodev.fetch2:xfetch2:3.1.5'
	implementation 'androidx.tonyodev.fetch2okhttp:xfetch2okhttp:3.1.5'

	// exo player
	def exoplayer_version = "2.15.1"
	implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
	implementation "com.google.android.exoplayer:exoplayer-hls:$exoplayer_version"
	implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"
	implementation "com.google.android.exoplayer:extension-mediasession:$exoplayer_version"

	// retrofit rest client
	implementation 'com.squareup.retrofit2:retrofit:2.9.0'
	implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

	// ACRA crash reporting
	def acra_version = "5.8.4"
	implementation "ch.acra:acra-mail:$acra_version"
	implementation "ch.acra:acra-dialog:$acra_version"

	// timber logging
	implementation 'com.jakewharton.timber:timber:4.7.1'

	// joda time
	implementation 'joda-time:joda-time:2.10.10'

	// about libraries view
	implementation "com.mikepenz:aboutlibraries:$about_libraries_version"

	// koin dependency injection
	implementation "io.insert-koin:koin-android:$koin_version"
	androidTestImplementation "io.insert-koin:koin-test:$koin_version"
	testImplementation "io.insert-koin:koin-test:$koin_version"
}
